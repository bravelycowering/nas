include os/bravelycowering+lib

using local_packages
using quit_resets_runargs
using no_runarg_underscore_conversion

#MOVABLE[141]
#MOVABLE[142]
#MOVABLE[143]
#MOVABLE[602]
#MOVABLE[603]

#SWAPPABLE[0]
#SWAPPABLE[8]
#SWAPPABLE[9]
#SWAPPABLE[10]
#SWAPPABLE[11]

#HEAVY[216]
#HEAVY[217]
#HEAVY[218]
#HEAVY[219]
#HEAVY[656]
#HEAVY[759]

#FALLS[141]
#FALLS[142]
#FALLS[143]
#FALLS[602]
#FALLS[603]

#UNSTABLE[461]

#onJoin
	clickevent sync register #onClick
	set coins 0
	set flowertalkprogress 0
	set flowertalkstate monologue
	// set push constants
	set PUSH[AwayX] -1 0 0
	set PUSH[TowardsX] 1 0 0
	set PUSH[AwayZ] 0 0 -1
	set PUSH[TowardsZ] 0 0 1
	// set pull constants
	set PULL[AwayX] 1 0 0
	set PULL[TowardsX] -1 0 0
	set PULL[AwayZ] 0 0 1
	set PULL[TowardsZ] 0 0 -1
	// set direction faces
	set DIRFACE[0] AwayZ
	set DIRFACE[1] TowardsX
	set DIRFACE[2] TowardsZ
	set DIRFACE[3] AwayX
	// set movement statistics
	set MOVESTAT[141] bench
	set MOVESTAT[142] crate
	set MOVESTAT[143] barrel
	set MOVESTAT[602] barrel
	set MOVESTAT[603] barrel
	// set movable transforms
	set TRANSFORM[143][AwayX] 602
	set TRANSFORM[143][TowardsX] 602
	set TRANSFORM[143][AwayZ] 603
	set TRANSFORM[143][TowardsZ] 603
	set TRANSFORM[602][AwayX] 143
	set TRANSFORM[602][TowardsX] 143
	set TRANSFORM[603][AwayZ] 143
	set TRANSFORM[603][TowardsZ] 143
	// set statistics
	set statistics.moved.barrel 0
	set statistics.moved.crate 0
	set statistics.moved.bench 0
	set statistics.chests 0
	set statistics.interact.door 0
	set statistics.interact.lantern 0
	set statistics.interact.computer 0
	set statistics.interact.flowers 0
	// set help texts
	set HELP.ICON &r(&fi&r)&7
	set HELP.GENERIC.MOVE This type of block is movable. Right click to push, left click to pull.
	set HELP.BLOCK[141] {HELP.GENERIC.MOVE}
	set HELP.BLOCK[142] {HELP.GENERIC.MOVE}
	set HELP.BLOCK[143] {HELP.GENERIC.MOVE}
	set HELP.BLOCK[602] {HELP.GENERIC.MOVE}
	set HELP.BLOCK[603] {HELP.GENERIC.MOVE}
	// misc contsts
	set DEFAULTREWARD coins|1
	set FLOWERTALK Hi!|Don't mind us :)|Just some regular talking flowers!|We know historically talking flowers aren't the most trustworthy, but we're just doing our job.|...Are you curious about that?|Well, since you clicked on us, we'll assume you already know how to move crates and barrels.|You may have also figured out that you cant push them if another crate is in front of them.|It's just too heavy!|As it turns out, if you're clever you can use the barrels to trap people inside this house.|The map creator has to come by and unblock the entrances when that happens.|That's where we come in! :)|Our job is to not let any movable objects pass.|If they try, we ask them nicely not to, and usually that works!|:)|...|Golly, we can't imagine talking to some flowers is that exciting.|Especially not here!|We appreciate you taking the time to listen to what we had to say, but...|Don't you have anything better to do?
	setsplit FLOWERTALK |
	// setup item related things
	set Item:itemsCmdTip /in
	set Item:lookCmdTip /in look
	set Item:dropCmdTip /in drop
	call #Item:define|TEST_ITEM\\Looks like some kind of small white dog.
	call #Item:define|IMPORTANT_TEST_ITEM!\\This seems kind of important...
	call #Item:define|CHEST_LID
	call #Item:define|EGG!
	// setup level permissions
	set l_lvlname |{LevelName}
	set l_playername |@p
	if l_lvlname|has|l_playername set manager true
	else set manager false
	if manager msg &aYou've been detected as the owner of this OS map!
quit

#input
	ifnot manager jump #input._skipadmincmds
		if runArg1|=|"reset" jump #reset
		if runArg1|=|"give" jump #Item:give|{runArg2}
		if runArg1|=|"take" jump #Item:take|{runArg2}
		ifnot runArg1|=|"set" jump #input._endset
			set {runArg2}
			setsplit runArg2 " "
			show {runArg2[0]}
			quit
		#input._endset
		ifnot runArg1|=|"show" jump #input._endshow
			show {runArg2}
			quit
		#input._endshow
		ifnot runArg1|=|"*" jump #input._endshowall
			show every single package
			quit
		#input._endshowall
		ifnot runArg1|=|"test" jump #input._endtest
			if label #Test:runset jump #Test:runset|{runArg2}
		#input._endtest
	#input._skipadmincmds
	ifnot runArg3|=|"" msg &cYou think you're so sneaky, don't you?
	ifnot runArg3|=|"" quit
	if runArg1|=|"" jump #Item:items
	if runArg1|=|"look" jump #Item:look|{runArg2}
	if runArg1|=|"drop" jump #Item:drop|{runArg2}
	msg &cUnknown input subcommand &7"{runArg1}"
quit

#reset
	resetdata packages
	resetdata locals
	if label #onJoin jump #onJoin
quit

#showStats
	if coins|=|1 set coinword coin
	else set coinword coins
	msg &uYou have &f{coins}&6 imaginary {coinword}&u.
	msg &uHere's some stats:
	msg &u  - Computers read:&a {statistics.interact.computer}
	msg &u  - Barrels rolled:&a {statistics.moved.barrel}
	msg &u  - Crates moved:&a {statistics.moved.crate}
	msg &u  - Benches misplaced:&a {statistics.moved.bench}
	msg &u  - Chests discovered:&a {statistics.chests}
	msg &u  - Doors knocked:&a {statistics.interact.door}
quit

#onClick
	setblockid clickedID {click.coords}
	if click.button|=|"Middle" jump #onMiddleClick
	setblockmessage clickedMSG {click.coords}
	ifnot clickedMSG|=|"" quit
	if label #onClickBlock[{clickedID}] jump #onClickBlock[{clickedID}]
jump #on{click.button}Click

// chests
#onClickBlock[216]
#onClickBlock[217]
#onClickBlock[218]
#onClickBlock[219]
	set coords {click.coords}
jump #chest.preCoorded|{DEFAULTREWARD}

#setChestReward
	setblockid myID {coords}
	if $reward|=|DEFAULTREWARD placemessageblock {myID} {coords}
	else placemessageblock {myID} {coords} /oss #chest|{$reward}
	set $reward
quit

#chest
	set coords {MBCoords}
	#chest.preCoorded
		ifnot $reward|=|"" jump #setChestReward
		// split the coords
		setsplit coords " "
		// dont re-open chest if already opened
		if chest_{coords[0]}_{coords[1]}_{coords[2]} quit
		// open chest
		setadd statistics.chests 1
		set chest_{coords[0]}_{coords[1]}_{coords[2]} true
		tempblock 624 {coords}
		setrandrange variant 2 13
		cs pos {coords} wood:choose({variant}):volume(2)
	if label #chestReward:{runArg1} jump #chestReward:{runArg1}
	effect puff {coords[0]} {coords[1]} {coords[2]} 0 -1 0
	msg The chest is empty... bummer!
quit

#chestReward:coins
	if runArg2|=|1 msg You just found &f1 &6imaginary coin&7!
	else msg You just found &f{runArg2} &6imaginary coins&7!
	setrandrange variant 2 4
	cs pos {coords} coin:choose({variant})
	setadd coins {runArg2}
	setadd coords[1] 0.75
	effect coin {coords[0]} {coords[1]} {coords[2]} 0 -1 0
	ifnot runArg2|>|1 quit
	set l_i 1
	#chestReward:coins.loop
		setadd l_i 1
		setrandrange l_x -10 10
		setrandrange l_z -10 10
		effect coin {coords[0]} {coords[1]} {coords[2]} {l_x} -10 {l_z}
	if l_i|<|runArg2 jump #chestReward:coins.loop
quit

#chestReward:item
	set l_itemid {runArg2}
	cs pos {coords} item bag pickup
	setadd coords[1] 0.75
	call #Item:sethas|l_has|{l_itemid}
	if l_has effect puff {coords[0]} {coords[1]} {coords[2]} 0 -1 0
	else effect exclamation {coords[0]} {coords[1]} {coords[2]} 0 -1 0
	if l_has msg &7You've already found the item that was here!
	call #Item:give|{l_itemid}
quit

#onClickBlock[762]
	cs pos {click.coords} computercalculatefinish
	setadd statistics.interact.computer 1
	cmd msgme &fThe computer says:&u jokerdril
	jump #showStats
quit

#onClickBlock[656]
	placeblock 759 {click.coords}
	cs pos {click.coords} click:choose(3):volume(0.5)
	setadd statistics.interact.lantern 1
quit

#onClickBlock[759]
	placeblock 656 {click.coords}
	cs pos {click.coords} click:choose(3):pitch(1.5):volume(0.5)
	setadd statistics.interact.lantern 1
quit

#onClickBlock[755]
#onClickBlock[756]
#onClickBlock[757]
#onClickBlock[758]
	setsub clickedID 4
	placeblock {clickedID} {click.coords}
	cs pos {click.coords} click:choose(3):volume(0.5)
quit

#onClickBlock[751]
#onClickBlock[752]
#onClickBlock[753]
#onClickBlock[754]
	setadd clickedID 4
	placeblock {clickedID} {click.coords}
	cs pos {click.coords} click:choose(3):pitch(1.5):volume(0.5)
quit

#onClickBlock[55]
#onClickBlock[760]
#onClickBlock[761]
	localcs pos {click.coords} knocking
	setadd statistics.interact.door 1
quit

#onClickBlock[96]
	if click.coords|=|PlayerCoords jump #flowersteppedon
	if flowertalkstate|=|"monologue" jump #flowermonologue
	if flowertalkstate|=|"steppedon" jump #flowerwassteppedon
	msg &fThe flowers say: &kWe aren't quite sure what happened, but we're not supposed to be saying this. Maybe this is some kind of error handling message..?
	set flowertalkstate monologue
quit

#flowermonologue
	if flowertalkprogress|=|12 setadd statistics.interact.flowers 1
	msg &fThe flowers say: &k{FLOWERTALK[{flowertalkprogress}]}
	setadd flowertalkprogress 1
	if flowertalkprogress|>=|FLOWERTALK.Length setsub flowertalkprogress 1
quit

#flowersteppedon
	set flowertalkstate steppedon
	setrandrange variant 1 3
	if variant|=|1 msg &fThe flowers say: &kWe'd appreciate it if you didn't step on us while we talk.
	if variant|=|2 msg &fThe flowers say: &kIt's kinda hard to see you when you're standing on us like this.
	if variant|=|3 msg &fThe flowers say: &kCould you step off of us please?
quit

#flowerwassteppedon
	set flowertalkstate monologue
	msg &fThe flowers say: &kThanks! :)
quit

#onLeftClick
	if label #MOVABLE[{clickedID}] jump #onMoveClick|PULL
quit

#onRightClick
	if label #MOVABLE[{clickedID}] jump #onMoveClick|PUSH
quit

#onMoveClick
	set face {click.face}
	ifnot face|=|"AwayY" jump #ifnot_face_is_AwayY__end
		set dir {PlayerYaw}
		setdiv dir 90
		setround dir
		setmod dir 4
		set face {DIRFACE[{dir}]}
	#ifnot_face_is_AwayY__end
	set moveby {{runArg1}[{face}]}
	setblockid myID {click.coords}
	set potentialTransform {TRANSFORM[{myID}][{face}]}
	cs pos {click.coords} wood stepleft:volume(2)
	ifnot potentialTransform|=|"" set myID {potentialTransform}
	ifnot moveby|=|"" jump #tryMoveBy|{myID}|{click.coords}|{moveby}
quit

#onMiddleClick
	if HELP.BLOCK[{clickedID}]|=|"" msg No help/tip is available for this block.
	else msg {HELP.ICON} {HELP.BLOCK[{clickedID}]}
quit

#tryMoveBy
	set moveto {runArg2}
	set moveby {runArg3}
	setsplit moveto " "
	setsplit moveby " "
	setadd moveto[0] {moveby[0]}
	setadd moveto[1] {moveby[1]}
	setadd moveto[2] {moveby[2]}
jump #tryMove|{runArg1}|{runArg2}|{moveto[0]} {moveto[1]} {moveto[2]}

#tryMove
	set myID {runArg1}
	set coords {runArg2}
	setsplit coords " "
	setadd coords[1] 1
	setblockid above {coords[0]} {coords[1]} {coords[2]}
	if label #HEAVY[{above}] quit
	if label #FALLS[{above}] quit
	setblockid movetoID {runArg3}
	ifnot label #SWAPPABLE[{movetoID}] quit
	set floorcoords {runArg3}
	setsplit floorcoords " "
	setsub floorcoords[1] 1
	setblockid floorID {floorcoords[0]} {floorcoords[1]} {floorcoords[2]}
	if label #SWAPPABLE[{floorID}] quit
	if label #UNSTABLE[{floorID}] quit
	ifnot label #MOVABLE[{clickedID}] quit
	placeblock {movetoID} {runArg2}
	placeblock {myID} {runArg3}
	ifnot MOVESTAT[{myID}]|=|"" setadd statistics.moved.{MOVESTAT[{myID}]} 1
quit
