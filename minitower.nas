using local_packages

include os/shinyiris+towerlib

#onJoin
	call #CTOHLib_Init
	set ctohlib.error.not.in.parkour.practice.mode &cYou aren't allowed to use practice mode right now
	zonechangedevent async register #onZoneChanged
	if label #map:{LevelName} call #map:{LevelName}
	msg You can enable setting checkpoints by pressing &aP&7 (or by typing &a/in practice&7)
	definehotkey practice|P
	definehotkey reset|R
	jump #resetTime
quit

#onZoneChanged
	if zoneMotd|has|"+runlabel" call #callZone
jump #CTOHLib_OnZoneChange

#callZone
	set zoneName {zone}
	setsplit zoneName #
	if zoneName.Length|=|1 jump {zoneName[0]}
jump {zoneName[1]}

#type
	set slot {runArg1}
	set text {runArg2}
	set showntext
	setsplit text
	set i 0
	#typeLoop
		delay 25
		set showntext {showntext}{text[{i}]}
		cs me typewriter noise:cut(0.1):speed(2)
		cpemsg {slot} {showntext}
		setadd i 1
	if i|<|{text.Length} jump #typeLoop
quit

#map:bravelycowering+minitower
	set ctohlib.is.in.parkour true
	msg welcome to probably the most annoying map you will play today
quit

#map:bravelycowering+minitower2
	set ctohlib.is.in.parkour true
	msg i felt bad for making the last one so hard, so ill give you an &aextra mid air jump&7 to beat this one
quit

#minitower4:fakeminitower
	tempchunk 25 67 20 35 82 30 270 90 211
	env fog D36538
	env sky 836668
	env clouds 836668
	env sun 525163
	env shadow 30304B
	cmd tp 275 92 216 0 0
	setspawn 275 92 216 0 0
	setdeathspawn 275 92 216 0 0
	call #map:bravelycowering+minitower
	set ctohlib.is.in.parkour false
	set minitower4:OpenCeilingPart #minitower4:removefakeminitower
quit

#minitower4:removefakeminitower
	tempchunk 270 90 211 280 105 221 270 90 211
quit

#map:bravelycowering+minitower4
	tempchunk 245 0 189 263 9 205 261 80 208
	call #minitower4:fakeminitower
	definehotkey practice|P
	definehotkey reset|R
	motd -hax -push -slap model=humanoid|0.5 jumpheight=0.6
	call #resetTime
	// create fires
	call #minitower4:CreateSidewaysFire|264 66 209
	call #minitower4:CreateSidewaysFire|270 67 209
	call #minitower4:CreateSidewaysFire|274 68 209
	call #minitower4:CreateSidewaysFire|266 70 209
	call #minitower4:CreateSidewaysFire|265 72 209
	call #minitower4:CreateSidewaysFire|274 72 209
	call #minitower4:CreateSidewaysFire|267 73 209
	call #minitower4:CreateSidewaysFire|270 74 209
	call #minitower4:CreateSidewaysFire|274 75 209
	call #minitower4:CreateSidewaysFire|277 75 209
	call #minitower4:CreateSidewaysFire|266 76 209
	call #minitower4:CreateSidewaysFire|273 77 209
	call #minitower4:CreateSidewaysFire|267 78 209
	call #minitower4:CreateSidewaysFire|275 78 209
	terminate
quit

#minitower4:Freeze
	if minitower4:Frozen quit
	if minitower4:preventFreezing quit
	env reset
	boost 0 0 0 1 0 1
	set minitower4:Frozen true
	freeze
quit

#minitower4:Reveal
	ifnot minitower4:Frozen quit
	set ctohlib.is.in.parkour true
	env reset
	set minitower4:Frozen false
	unfreeze
	tempblock 711 271 80 216
	setspawn {PlayerCoords} 0 0
	setdeathspawn {PlayerCoords} 0 0
	motd ignore
	msg you can keep your &aextra mid air jump&7 for this one as well
	msg You can enable setting checkpoints by pressing &aP&7 (or by typing &a/in practice&7)
	call #resetTime
	call #type|announce|MINITOWER /// THIRD
	delay 350
	call #type|bigannounce|DISINTEGRATION LOOP
	cpemsg announce MINITOWER /// THIRD
quit

#minitower4:CreateSidewaysFire
	setadd minitower4:FireCount 1
	set botid mt4fire_{minitower4:FireCount}
	set coords {runArg1}
	setsplit coords " "
	setsub coords[2] 0.5
	setadd coords[1] 0.5
	cmd tempbot add {botid} {coords[0]} {coords[1]} {coords[2]} 0 0 0 empty
	cmd tempbot model {botid} 54
	cmd tempbot rot {botid} x -90
quit

#minitower4:OpenWaterfallDoor
	tempchunk 254 65 219 255 67 219 254 65 218
	tempblock 722 261 69 221
quit

#minitower4:OpenCeiling
	set minitower4:preventFreezing true
	tempchunk 261 80 208 279 90 224 261 80 208
	jump {minitower4:OpenCeilingPart}
quit

#resetAllData
	placemessageblock air 0 1 0
quit

#showClearList
	setblockmessage data 0 1 0
	setsplit data ||
	set clears {data[0]}
	setsplit clears |
	if clears.Length|=|0 jump #showEmptyClearList
	set n {runArg1}
	if n|<|1 set n 1
	if n|>|clears.Length set n {clears.Length}
	msg Clears in order of completion:
	setsub n 1
	set start {n}
	setadd start 1
	set max {n}
	setadd max 10
	if max|>|clears.Length set max {clears.Length}
	#showClearListLoop
		set user {clears[{n}]}
		setsplit user :
		setadd n 1
		msg   {n}. {user[1]}
	if n|<|max jump #showClearListLoop
	setadd n 1
	set clearCountMsg Showing clears {start}-{max} (out of {clears.Length})
	if max|<|clears.Length set clearCountMsg {clearCountMsg} Next: &a/in clears {n}
	msg {clearCountMsg}
quit

#showEmptyClearList
	msg No one has beaten this map before, complete it to take the first clear!
quit

#tryAddSelfToClearList
	setblockmessage data 0 1 0
	setsplit data ||
	set clears {data[0]}
	set {runArg1} false
	if clears|has|"@p:" quit
	set clears {clears}|@p:@color@nick
	placemessageblock air 0 1 0 {clears}
	setlength clears |
	set {runArg1} {clears.Length}
quit

#reset
	kill
#resetTime
	set beaten false
	set startMS {epochMS}
quit

#input
	if runArg1|=|"clears" jump #showClearList|{runArg2}
	if runArg1|=|"practice" jump #CTOHLib_TogglePracticeMode
	if ctohlib.is.in.practice.mode quit
	if runArg1|=|"reset" jump #reset
quit

#CTOHLib_Trigger_PracticeModeOn
	msg Practice mode: &aON
quit

#CTOHLib_Trigger_PracticeModeOff
	msg Practice mode: &cOFF
jump #resetTime

#parseTime
	setdiv {runArg1} 1000
	set {runArg1} {{runArg1}}s
quit

#getNSuffixOverride[11]
#getNSuffixOverride[12]
#getNSuffixOverride[13]

#getNSuffix
	set n {{runArg1}}
	if label #getNSuffixOverride[{n}] jump #getNSuffix_3
	setmod n 10
	ifnot n|=|1 jump #getNSuffix_1
		set {runArg1} {{runArg1}}st
		quit
	#getNSuffix_1
	ifnot n|=|2 jump #getNSuffix_2
		set {runArg1} {{runArg1}}nd
		quit
	#getNSuffix_2
	ifnot n|=|3 jump #getNSuffix_3
		set {runArg1} {{runArg1}}rd
		quit
	#getNSuffix_3
	set {runArg1} {{runArg1}}th
quit

#win
	allowmbrepeat
	if beaten quit
	set beaten true
	if ctohlib.is.in.practice.mode jump #winPractice
	set final {epochMS}
	setsub final {startMS}
	call #parseTime|final
	call #tryAddSelfToClearList|clearNumber
	if clearNumber|=|"false" jump #localmsgClearNumber_end
		call #getNSuffix|clearNumber
		localmsg chat @color@p&7 becaome the &6{clearNumber}&7 person to complete &b{LevelName}&7!
	#localmsgClearNumber_end
	cpemsg announce &aCongrats on making it to the top!
	cpemsg smallannounce &fYou had a time of &6{final}&f.
	msg &fYou completed &b{LevelName}&7 in &6{final}&f.
quit

#winPractice
	cpemsg announce Congrats on making it to the top!
	cpemsg smallannounce &bNow complete it without any checkpoints.
	jump #CTOHLib_TogglePracticeMode
quit
